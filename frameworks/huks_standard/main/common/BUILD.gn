# Copyright (C) 2021-2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/security/huks/build/config.gni")
import("//build/ohos.gni")

config("huks_config") {
  include_dirs = [
    "include",
    "//base/security/huks/interfaces/innerkits/huks_standard/main/include",
  ]
}

if (os_level == "standard") {
  ohos_static_library("libhuks_common_standard_static") {
    subsystem_name = "security"
    part_name = "huks"

    defines = [
      "_HARDWARE_ROOT_KEY_",
      "_HUKS_LOG_ENABLE_",
      "L2_STANDARD",
    ]
    public_configs =
        [ ":huks_config" ]  # Share include files for other gn when deps.

    include_dirs = [
      "//base/security/huks/interfaces/innerkits/huks_standard/main/include",
    ]

    sources = [
      "src/hks_ability.c",
      "src/hks_base_check.c",
      "src/hks_check_paramset.c",
      "src/hks_common_check.c",
      "src/hks_crypto_adapter.c",
      "src/hks_param.c",
    ]

    cflags = [
      "-DHILOG_ENABLE",
      "-Wall",
      "-Werror",
    ]
    complete_static_lib = true

    sanitize = {
      integer_overflow = true
      cfi = true
      debug = false
    }
    configs = [ "//base/security/huks/frameworks/config/build:coverage_flag" ]
    sources += [ "src/hks_errcode_adapter.c" ]
    external_deps = [ "hiviewdfx_hilog_native:libhilog" ]
  }
} else {
  ohos_static_library("libhuks_common_small_static") {
    defines = [
      "_HUKS_LOG_ENABLE_",
    ]
    public_configs =
        [ ":huks_config" ]  # Share include files for other gn when deps.

    include_dirs = [
      "//base/security/huks/interfaces/innerkits/huks_standard/main/include",
    ]

    sources = [
      "src/hks_ability.c",
      "src/hks_base_check.c",
      "src/hks_check_paramset.c",
      "src/hks_common_check.c",
      "src/hks_crypto_adapter.c",
      "src/hks_param.c",
    ]

    cflags = [
      "-DHILOG_ENABLE",
      "-Wall",
      "-Werror",
      "-fPIC",
    ]
    if (huks_use_hardware_root_key == true) {
      cflags += [ "-D_HARDWARE_ROOT_KEY_" ]
    }
    complete_static_lib = true
    cflags += [ "-DHUKS_UID_WHITE_LIST=${huks_uid_white_list_define}" ]

    deps = [ "//base/hiviewdfx/hilog_lite/frameworks/featured:hilog_shared" ]

    sources += [ "src/hks_check_white_list.c" ]

    if (huks_config_file != "") {
      print(huks_config_file)
      cflags += [ "-DHKS_CONFIG_FILE=\"${huks_config_file}\"" ]
    }
  }
}
